buildscript {
    ext {
        springPlatformVersion = '4.3.8.RELEASE'
        // springPlatformVersion = 'Athens-SR5'
        // orientDBVersion = '2.2.22-SNAPSHOT'
        orientDBVersion = '2.2.29'
    }
    repositories {
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath 'io.spring.gradle:propdeps-plugin:0.0.9.RELEASE'
    }
}
plugins {
    id 'io.spring.dependency-management' version '1.0.3.RELEASE'
}


allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'propdeps-eclipse'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.7
    version = '1.0.0-SNAPSHOT'
    group = 'org.springframework.data'

    repositories {
        mavenLocal()
        maven { url 'https://repo.spring.io/libs-release' }
        maven { url 'https://repo.spring.io/libs-snapshot' }
        mavenCentral()
    }
    dependencyManagement {
        imports {

            mavenBom "io.spring.platform:platform-bom:$springPlatformVersion"
        }
        dependencies {
            dependency 'org.codehaus.groovy:groovy-jsr223:2.4.11'
            dependency 'org.codehaus.groovy:groovy-all:2.4.11'
            dependency "com.orientechnologies:orientdb-lucene:$orientDBVersion"
            dependency "com.orientechnologies:orientdb-graphdb:$orientDBVersion"
            dependency "com.orientechnologies:orientdb-spatial:$orientDBVersion"
            dependency 'com.tinkerpop.blueprints:blueprints-core:2.6.0'
            dependency 'com.tinkerpop.gremlin:gremlin-java:2.6.0'
            dependency 'com.tinkerpop.gremlin:gremlin-groovy:2.6.0'
        }
    }
    dependencies {
        compile 'org.springframework:spring-beans'
        compile 'org.springframework:spring-context'
        compile 'org.springframework:spring-tx'
        compile 'org.springframework.data:spring-data-commons'
        compile 'org.codehaus.groovy:groovy-jsr223'
        compile 'org.codehaus.groovy:groovy-all'
        compile 'com.tinkerpop.blueprints:blueprints-core'
        compile 'com.tinkerpop.gremlin:gremlin-java'
        compile 'com.tinkerpop.gremlin:gremlin-groovy'
        compile 'org.apache.commons:commons-collections4:4.0'
        compile 'org.apache.commons:commons-lang3:3.4'
        compile 'org.reflections:reflections:0.9.9-RC1'
        testCompile 'junit:junit:4.12'
        testCompile 'org.mockito:mockito-core:1.10.19'
        testCompile 'org.springframework:spring-test'
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    javadoc {
        failOnError = false
    }
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
    if (rootProject.hasProperty('releaseBuild') && rootProject.ext.releaseBuild.toBoolean()) {
        apply from: "$rootProject.path/release.gradle"
    }
}



